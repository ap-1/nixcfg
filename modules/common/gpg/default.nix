{ inputs, ... }:
let
  # nixpkgs sequoia-sq uses the pqc-less nettle backend; pull the pending 1.4.0 PR branch and build on openssl
  sqPqcOverlay = _: prev: {
    sequoia-sq =
      inputs.nixpkgs-sequoia.legacyPackages.${prev.stdenv.hostPlatform.system}.sequoia-sq.overrideAttrs
        (_: {
          cargoBuildNoDefaultFeatures = true;
          cargoBuildFeatures = [ "crypto-openssl" ];
          cargoCheckNoDefaultFeatures = true;
          cargoCheckFeatures = [ "crypto-openssl" ];
        });
  };

  # crypto-rust backend and a bumped lock pull sequoia-openpgp 2.4, so gpg-sq handles pqc keys
  chameleonPqcOverlay = _: prev: {
    sequoia-chameleon-gnupg = prev.sequoia-chameleon-gnupg.overrideAttrs (old: {
      nativeBuildInputs = (old.nativeBuildInputs or [ ]) ++ [ prev.capnproto ];
      cargoDeps = prev.rustPlatform.importCargoLock {
        lockFile = ./Cargo.lock;
      };
      postPatch = (old.postPatch or "") + ''
        cp ${./Cargo.lock} Cargo.lock
      '';
      cargoBuildNoDefaultFeatures = true;
      cargoBuildFeatures = [
        "sequoia-openpgp/crypto-rust"
        "sequoia-openpgp/allow-experimental-crypto"
        "sequoia-openpgp/allow-variable-time-crypto"
      ];
    });
  };
in
{
  flake.modules.nixos.gpg.nixpkgs.overlays = [ sqPqcOverlay chameleonPqcOverlay ];
  flake.modules.darwin.gpg.nixpkgs.overlays = [ sqPqcOverlay chameleonPqcOverlay ];

  flake.modules.homeManager.gpg =
    { pkgs, ... }:
    {
      home.packages = with pkgs; [
        sequoia-sq
        sequoia-chameleon-gnupg
      ];

      services.gpg-agent = {
        enable = true;
        enableZshIntegration = true;
        defaultCacheTtl = 86400;
        maxCacheTtl = 86400;
        pinentry.package = if pkgs.stdenv.isDarwin then pkgs.pinentry_mac else pkgs.pinentry-curses;
      };
    };
}
